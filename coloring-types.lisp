;; coloring-types.lisp

(in-package :colorize)

(eval-when (:compile-toplevel :load-toplevel :execute)
  (defparameter *version-token* (gensym)))

(defparameter *symbol-characters*
  "abcdefghijklmnopqrstuvwxyzABCDEFGHIJKLMNOPQRSTUVWXYZ*!%$&+-1234567890<=>")

(defparameter *non-constituent*
  '(#\space #\tab #\newline #\linefeed #\page #\return
    #\" #\' #\( #\) #\, #\; #\` #\[ #\]))

(defparameter *special-forms*
  '("let" "load-time-value" "quote" "macrolet" "progn" "progv" "go" "flet" "the"
    "if" "throw" "eval-when" "multiple-value-prog1" "unwind-protect" "let*"
    "labels" "function" "symbol-macrolet" "block" "tagbody" "catch" "locally"
    "return-from" "setq" "multiple-value-call"))

(defparameter *common-macros*
  '("loop" "cond" "lambda"))

(defparameter *open-parens* '(#\())
(defparameter *close-parens* '(#\)))

(define-coloring-type :lisp "Basic Lisp"
  :default-mode :first-char-on-line
  :invisible t
  :transitions
  (((:in-list)
    ((or
      (scan-any *symbol-characters*)
      (and (scan #\.) (scan-any *symbol-characters*))
      (and (scan #\\) (advance 1)))
     (set-mode :symbol
               :until (scan-any *non-constituent*)
               :advancing nil))
    ((or (scan #\:) (scan "#:"))
     (set-mode :keyword
               :until (scan-any *non-constituent*)
               :advancing nil))
    ((scan "#\\")
     (let ((count 0))
       (set-mode :character
                 :until (progn
                          (incf count)
                          (if (> count 1)
                              (scan-any *non-constituent*)))
                 :advancing nil)))
    ((scan #\")
     (set-mode :string
               :until (scan #\")))
    ((scan #\;)
     (set-mode :comment
               :until (scan #\newline)))
    ((scan "#|")
     (set-mode :multiline
               :until (scan "|#")))
    ((scan #\()
     (set-mode :in-list
               :until (scan #\)))))
   ((:normal :first-char-on-line)
    ((scan #\()
     (set-mode :in-list
               :until (scan #\)))))
   (:first-char-on-line
    ((scan #\;)
     (set-mode :comment
               :until (scan #\newline)))
    ((scan "#|")
     (set-mode :multiline
               :until (scan "|#")))
    ((advance 1)
     (set-mode :normal
               :until (scan #\newline))))
   (:multiline
    ((scan "#|")
     (set-mode :multiline
               :until (scan "|#"))))
   ((:symbol :keyword :escaped-symbol :string)
    ((scan #\\)
     (let ((count 0))
       (set-mode :single-escaped
                 :until (progn
                          (incf count)
                          (if (< count 2)
                              (advance 1))))))))
  :formatter-variables ((paren-counter 0))
  :formatter-after-hook (lambda nil
                          (format nil "窿祜镳骘骝镯疳蝈瞽泔躅翦滹黝麸泔祆邈⒓箴犷炯箴犷劲┅烘矧磲趑弪èê铒蝽犰烘轵篝汨狎镱扉铄灬礅溽豉疱螬ㄤ邈灬蝈ㄩ珙矧豉疱┅螬è洪瞽扉篝灬礅溽豉疱螬ㄤ邈灬蝈ㄩ珙矧豉疱┅灬忮祗è泔祜颦疳蝈铙螬戾è疳蝈瞽痫ㄦ轭洵殒铒＇铛祆磲疸狎＇灬礅溽ㄣ痫箝糸镱螬ㄡ痧孱镳孱疳蝈铙沆矬瀛疳蝈铙┅┅ㄩ疳蝈瞽痫戾è忮骘蝈疳蝈篚怏羼疳蝈瞽痫螬ㄡ骠弪疳蝈篚怏羼ū疳蝈瞽痫螬┅疳蝈ㄥ祠疳蝈瞽痫螬镳孱铋飑ㄣ秕铘癌麒孱礤礅弪疳蝈镳孱疳蝈铙呼弩＇汨狎僵箦翩泔躅盹疳蝈瞽泔躅翦订ㄩ钽疳蝈瞽泔躅翦颟箦翩镳孱舂麒孱礤礅弪疳蝈沆矬瀛疳蝈铙呼弩＇汨狎僵ㄤ邈疳蝈瞽泔躅翦颟ㄩ镳孱ㄦ矧磲铋良箴犷沆狍蠼堍疳蝈铪淋⒕眉箴犷沆狍蠼堍淋⒕立忮骘蝈疳蝈ū泔躅舂疳蝈泱蟓忉汶珧秕钿沆狍螵ㄣ镬矧疳蝈铙徭翦颦疳蝈瞟ㄦ矧磲铋良箴犷峻眉箴犷峻立忮骘蝈疳蝈疳蝈ㄣ镬矧疳蝈铙徭翦颦疳蝈瞟┅螬┅ㄣ镬矧疳蝈铙螬┅è后礅镬哄筱狃邃簌礅镬灬礅溽豉疱螬ㄤ邈灬蝈ㄩ珙矧豉疱┅戾舄è泔祜痫箝糸镱＼烘蝻憝孱舂铄鳝矧ㄡ钿泔祜篚怏羼ū泔祜瞟┅螬┅ㄣ镱è矧礤礅弪铄鳝泔眄镱磲泸矬呼弩＇篝蜷铉羼踽飑礤礅弪铄鳝箴邈獒飙骘蝽螵呼弩＇篝蜷铉羼踽飑箫礤＇灬礅溽ㄥㄡ钿戾铉翳铄鳝螬戾铉翳濠篝蜷铉羼踽篚怏羼铄鳝戾铉翳濠┅┅Ж⒆稍拳⒛牌┅ㄦ矧磲铋⒓榫俭疳沆狍蠼堍簌礅镬堍峻良箴犷炯榫螬è犷戾铉翳铄鳝螬博ㄣ栳蚪ㄥ祠铄鳝癌＼ㄣ栳蚪ㄥ祠铄鳝ū戾铉翳铄鳝螬┅＼┅ㄦ矧磲铋⒓箴犷沆狍蠼堍箴邈獒燔⒕良箴犷劲螬螬┅┅ê脲黠蜾灬礅溽豉疱螬ㄤ邈灬蝈ㄩ珙矧豉疱┅ㄦ矧磲铋⒓箴犷沆狍蠼堍脲黠蜾堍峻良箴犷劲螬┅è恒镯礤铘喉蹯糸扉铄灬礅溽豉疱螬ㄤ邈灬蝈ㄩ珙矧豉疱┅ㄦ矧磲铋⒓箴犷沆狍蠼堍泔眄孱糗⒕良箴犷劲螬┅è恒栳蜥泗弪灬礅溽豉疱螬ㄤ邈灬蝈ㄩ珙矧豉疱┅ㄦ矧磲铋⒓箴犷沆狍蠼堍汨狎徙翦蜍⒕良箴犷劲螬┅è后趄轭绌灬礅溽豉疱螬ㄤ邈灬蝈ㄩ珙矧豉疱┅ㄦ矧磲铋⒓箴犷沆狍蠼堍篝蜷铉堍峻良箴犷劲螬┅è后轭珈瀛弩汜疱洎灬礅溽豉疱螬ㄣ犰飙骘蝽狒翦ㄣ潋豉疱螬┅è后铘狲弪蝻颟灬礅溽豉疱螬ㄤ邈灬蝈ㄩ珙矧豉疱┅ㄦ矧磲铋⒓箴犷沆狍蠼堍簌铘狲弪蝻蜍⒕良箴犷劲螬┅┅ㄤ彐轭瀛泔祜蜷铉豉疱后汨屙⒂汨屙澧吼狎孱红轶呼蜥铙轸轱铙èê铒蝽犰洪瞽扉篝è筱犷箦舡盹溴后礅镬乎铘殪筱犷犷铒瞽泔铙糸趱孱舄横漩犷汩铉铋飑è筱犷＼郓箦舡盹溴洪瞽扉篝乎铘殪筱犷＼荸┅┅烘矧磲趑弪èê轭扉篝灬礅溽豉疱螬ㄤ邈灬蝈ㄩ珙矧豉疱螬戾è镳孱疳蝈铙ㄣ镱＼镳孱疳蝈铙┅í沆矬瀛疳蝈铙ㄣ镱＼沆矬瀛疳蝈铙┅ㄣ犰飙疳蝈铘骘蝽狒翦颟┅è后礅镬哄筱狃邃簌礅镬灬礅溽豉疱螬ㄤ邈灬蝈ㄩ珙矧豉疱┅戾è蝈篚祠ㄩㄦ轭洵疳汶徵候凋蟓祜镫躔ㄦ躅汜祆簌礅镬骢钽糸镱ㄩ铘弪⒂偻孪汰滔纤招候凋蟓祜镫躔┅螬┅ㄩ蝈篚祠ㄦ矧磲铋⒓栩彐杰淋沆狍蠼堍簌礅镬堍峻良峋蝈篚祠ㄣ犰飙疳蝈铘骘蝽狒翦颟ㄣ犰飙疳蝈铘骘蝽狒翦颟┅┅┅ㄤ彐轭瀛泔祜蜷铉豉疱哄扉箴⑴磲泱涕箴吼狎孱红轶烘矧磲趑弪èê簌礅镬哄筱狃邃簌礅镬灬礅溽豉疱螬ㄤ邈灬蝈ㄩ珙矧豉疱┅戾è蝈篚祠ㄩㄦ轭洵疳汶徵哄扉箴祜镫躔ㄦ躅汜祆簌礅镬骢钽糸镱ㄩ铘弪⒂偻孪汰滔纤招哄扉箴祜镫躔┅螬┅ㄩ蝈篚祠ㄦ矧磲铋⒓栩彐杰淋沆狍蠼堍簌礅镬堍峻良峋蝈篚祠ㄣ犰飙疳蝈铘骘蝽狒翦颟ㄣ犰飙疳蝈铘骘蝽狒翦颟┅┅┅ㄤ彐轭瀛泔祜蜷铉豉疱恒镯盹瞽扉箴⒚镯盹涕箴吼狎孱红轶呼蜥铙轸轱铙èê铒蝽犰洪瞽扉篝è筱犷＼箦舡盹溴哄筱狃邃簌礅镬乎铘殪筱犷＼┅┅烘矧磲趑弪èê簌礅镬哄筱狃邃簌礅镬灬礅溽豉疱螬ㄤ邈灬蝈ㄩ珙矧豉疱┅戾舄è泔祜痫箝糸镱＼烘蝻憝孱呼弩＇汨狎僵麸祜镫躔ㄩ泔祜篚怏羼ū泔祜瞟螬蝈篚祠ㄩㄦ轭洵疳汶徵恒扈蟓祜镫躔ㄦ躅汜祆簌礅镬骢钽糸镱ㄩ铘弪⒂偻孪汰滔纤招恒扈蟓祜镫躔┅麸祜镫躔┅┅ㄩ蝈篚祠ㄦ矧磲铋⒓栩彐杰淋沆狍蠼堍簌礅镬堍峻良峋蝈篚祠ㄣ犰飙疳蝈铘骘蝽狒翦颟ㄣ犰飙疳蝈铘骘蝽狒翦颟┅┅┅ㄤ彐轭瀛泔祜蜷铉豉疱恒镯盹瞽扉箴骈戾⒚镯盹涕箴崎戾吼狎孱恒镯盹瞽扉箴轰彐狨祠盹溴洪瞽扉篝洪铞轶殁戾舂ㄤ彐鲠悱镳孱疳蝈铙ㄛㄤ彐鲠悱沆矬瀛疳蝈铙┹ㄤ彐鲠悱蝈箦蝣邃黠蜾螵Ж⑨豸铫⑩蝈犭汜箦汨狎泔铙簪泔铘轭蹂溴驷蹯簪滹滹踱戾㈠祗澧㈠铛恝㈠翦蝾㈡祜狒㈡矧㈢雉铫㈤姊㈤铘㈧镱纰Ⅱ彗轶翦颌Ⅱ弭躜睥Ⅲ栾螋Ⅲ殓铄洧Ⅲ辁屣姊Ⅲ翎糸恽Ⅲ趄蹉簪Ⅲ鏖翥琚Ⅳ疱溴姊Ⅴ铋镱Ⅴ铙殓铄洧Ⅵ镩洧Ⅵ镬狒殪澧Ⅶ栝戾⑦唑弩趄殂簪⑦嘛镬┅ㄤ彐疳蜥礤翦悱忮玳瞽黠蜾⑨忏溴骁栝觌祉铒瘃蝮趱鲼谅媚牌侨墒颂臀闲岩釉罩棕仝甙辈炒刀犯耿ㄤ彐疳蜥礤翦悱翦蝽轭狒矧螵Ж＼箴徙＼蝈趱蝾＼翎＼铄黛轭＼＼＼＼＼＼＼＼＼＼＼＼＼＼＼＼）ㄤ彐轭瀛泔祜蜷铉豉疱衡狍殂⒙狍殂芒轰彐狨祠盹溴侯矧磲洪铞轶殁戾呼蜥铙轸轱铙è侯矧磲è筱犷犷悱忮玳瞽黠蜾箦舡盹溴瑚矧洵轶乎铘殪筱犷犷悱翦蝽轭狒矧螵横漩犷汩铉铋飑è筱犷箦舡盹溴恒镯礤铘乎铘殪筱犷┅è矧筱犷犷悱镳孱疳蝈铙筱犷犷悱沆矬瀛疳蝈铙┅箦舡盹溴吼狎孱轶乎铘殪ㄡ漩犷沐暴横漩犷汩铉铋飑è筱犷＼箦舡盹溴后趄轭乎铘殪筱犷＼┅è矧筱犷к堍筱犷＼З箦舡盹溴恒栳蜥泗弪乎铘殪ㄡ漩犷沐博┅ê篝蜷铉è筱犷＼堠箦舡盹溴后轭珈瀛弩汜疱乎铘殪ㄡ漩犷沐暴┅┅烘矧磲趑弪鲠蜷徕戾è疳蝈瞽泔躅翦癌烘矧磲趑弪徭翦颦栾镫灬礅溽铋ㄦ矧磲铋窿祜镳骘骝镯疳蝈瞽泔躅翦滹黝麸泔祆邈⒓箴犷炯箴犷劲┅烘矧磲趑弪è侯矧磲灬礅溽豉疱螬ㄤ邈灬蝈ㄩ珙矧豉疱┅螬ê泔眄孱灬礅溽豉疱螬ㄤ邈灬蝈ㄩ珙矧豉疱┅ㄦ矧磲铋⒓箴犷沆狍蠼堍泔眄孱糗⒕良箴犷劲螬┅ê篝蜷铉灬礅溽豉疱螬ㄤ邈灬蝈ㄩ珙矧豉疱┅ㄦ矧磲铋⒓箴犷沆狍蠼堍篝蜷铉堍峻良箴犷劲螬┅ê汨狎徙翦灬礅溽豉疱螬ㄤ邈灬蝈ㄩ珙矧豉疱┅ㄦ矧磲铋⒓箴犷沆狍蠼堍汨狎徙翦蜍⒕良箴犷劲螬┅ê箝铉戾弩汜疱灬礅溽豉疱螬ㄣ犰飙骘蝽狒翦ㄣ潋豉疱螬┅ê疳蝈瞽轶灬礅溽豉疱螬ㄤ邈灬蝈ㄩ珙矧豉疱┅戾è镳孱铋飑ㄣ秕铘癌ㄩㄥ耢戾铉翳螬暴痱镧麒孱礤礅弪ㄥ祠癌ㄣ镥蜚悱镳孱疳蝈铙ъ轶舂箦翩镳孱舂箦翩泔躅盹疳蝈瞽泔躅翦订ㄩ钽疳蝈瞽泔躅翦颟麒孱礤礅弪ㄥ祠癌ㄣ镥蜚悱沆矬瀛疳蝈铙ъ轶舂箦翩镳孱铋飑ㄤ邈疳蝈瞽泔躅翦颟箦翩泔躅盹疳蝈瞽泔躅翦订┅ㄩ镳孱ㄦ矧磲铋⒓箴犷沆狍蠼堍疳蝈铪淋⒕良箴犷沆狍蠼堍淋⒕ū泔躅舂泱蟓忉汶珧秕钿沆狍螵ㄦ矧磲铋⒓箴犷峻良箴犷劲螬┅螬┅ê黠蜾轶灬礅溽豉疱螬ㄤ邈灬蝈ㄩ珙矧豉疱┅ㄩ礤礅弪悱蝈箦蝣邃黠蜾螵呼弩＇篝蜷铉僵ㄦ矧磲铋⒓箴犷沆狍蠼堍簌礅镬堍峻良箴犷劲螬螬┅┅ㄤ彐轭瀛泔祜蜷铉豉疱恒⒚吼狎孱衡狍殂呼蜥铙轸轱铙è侯矧磲è筱犷＼）箦舡盹溴吼蝈痱镢弩箫乎铘殪筱犷犷Ж＼蝈趱蝾＼铄黛轭濠┅┅烘矧磲趑弪è吼蝈痱镢弩箫灬礅溽豉疱螬ㄤ邈灬蝈ㄩ珙矧豉疱┅ㄦ矧磲铋⒓箴犷沆狍蠼堍箴邈獒燔⒕良箴犷劲螬┅┅ㄤ彐鲠惬蝈箦蝣邃黠蜾螵Ж⑨箜⑨豸铫⑩镲膦⑩蝈犭汜箦汜翥琚汨狎沆狍螈泔铙簪泔铙暨汜篝泔铘轭蹂溴驷蹯簪溴戾翦滹滹踱戾澌钺黹氵汜篝㈠祗澧㈠铛恝㈠痨殂轸㈠痫螋㈠翦蝾㈡犰箦㈡祜狒㈡矧㈡蜷孱洧㈢雉铫㈤姊㈤铎轭澧㈤铘㈧镱纰㈨豸徕戾㈩犴弩疳沐㈩鬻镳弪狒矧痱轹狒澧痱雉邈翦洧瘐忪殂Ⅱ彗轶翦颌Ⅱ彘铘弪痱弭咩狍簪Ⅱ弭躜睥Ⅲ栾螋Ⅲ殓铄洧Ⅲ辁屣姊Ⅲ翎糸恽Ⅲ翎糸氵汜篝Ⅲ趄蹉簪Ⅲ鏖翥琚Ⅳ屙痨狒澧Ⅳ栝螈Ⅳ栩秣Ⅳ蝓澧Ⅳ蝙Ⅳ疱溴姊Ⅳ疱殇Ⅳ疱钺礤Ⅴ铋镱Ⅴ铙殓铄洧Ⅴ箝铉Ⅵ轵趱犰Ⅵ镩洧Ⅵ镬狒殪澧Ⅶ汨狎唪Ⅶ栝戾┅ㄤ彐轭瀛泔祜蜷铉豉疱恒⒚吼狎孱恒呼蜥铙轸轱铙è侯矧磲è筱犷箦舡盹溴恒镯礤铘乎铘殪筱犷犷Ж＼蝈趱蝾＼铄黛轭濠┅┅烘矧磲趑弪è瑚矧洵轶灬礅溽豉疱螬ㄤ邈灬蝈ㄩ珙矧豉疱┅ㄩ礤礅弪惬蝈箦蝣邃黠蜾螵呼弩＇篝蜷铉僵ㄦ矧磲铋⒓箴犷沆狍蠼堍簌礅镬堍峻良箴犷劲螬螬┅┅ㄤ彐鲠赆鲠蝈箦蝣邃黠蜾螵Ж⑨怏趄徙簪⑩镲戾犷⑩蝈犭⑩翦汜箦汜翥琚汨狎沆狍螈泔铙簪泔铘轭蹂溴驷蹯簪滹滹踱戾㈠祗澧㈠翦钿螈㈡轭犰㈡轭犰禊㈡祜狒㈡矧㈢雉铫㈤姊㈤眇戾礤铘螈㈤眇矧簪㈤铙翎钽屣姊㈤铘㈤铘弪驷沐㈧镱纰㈩狒轹澧㈩鬻疳汶徵澧痱轹狒澧痱雉邈翦洧瘐忪殂Ⅱ弭躜睥Ⅲ栾螋Ⅲ翎糸恽Ⅲ趄殂翩稷Ⅲ躔弪Ⅲ鏖翥琚Ⅲ钽栩镱辁邃Ⅳ栝螈Ⅳ栩秣Ⅳ栩秣螈Ⅳ蜥铙殄铘Ⅳ蝙Ⅵ镩洧Ⅵ镬狒殪澧Ⅶ栝戾┅ㄤ彐轭瀛泔祜蜷铉豉疱宏狯⑹狯幄吼狎孱恒烘矧磲趑弪è瑚矧洵轶灬礅溽豉疱螬ㄤ邈灬蝈ㄩ珙矧豉疱┅ㄩ礤礅弪赆鲠蝈箦蝣邃黠蜾螵呼弩＇篝蜷铉僵ㄦ矧磲铋⒓箴犷沆狍蠼堍簌礅镬堍峻良箴犷劲螬螬┅┅戾è翦蝽轭狒瀛铄铋飑ㄤ彐轭瀛泔祜蜷铉豉疱猴怅邈糸鲥⑾怅邈糸鲥芒呼蜥铙轸轱铙è侯矧磲è筱犷＼郓箦舡盹溴衡彗轭礤篌徵瀛箦钿乎铘殪ㄡ漩犷沐暴横漩犷汩铉铋飑è筱犷＼荸箦舡盹溴哄钿礤篌徵瀛箦钿乎铘殪ㄡ漩犷沐暴横漩犷汩铉铋飑è筱犷犷悱忮玳瞽黠蜾箦舡盹溴瑚矧洵轶乎铘殪矧ㄡ钿疱咫犷Ж＼憨箦翩翦蝽轭狒瀛铄舂ㄡ钿翦蝽轭狒瀛铄痱镧箦翩翦蝽轭狒瀛铄铋飑ㄡ漩犷沐暴┅筱犷犷悱翦蝽轭狒矧螵┅横漩犷汩铉铋飑┅ê黠蜾轶＋铋è筱犷＼憨ㄦ矧磲㈣辋ア箦舡盹溴瑚矧洵轶乎铘殪ㄡ漩犷沐暴横漩犷汩铉铋飑箦翩翦蝽轭狒瀛铄舂┅吼狎孱恒烘矧磲趑弪鲠蜷徕戾è轶脲黠蜾铋飑ㄩ瞽礤篌徵瀛箦钿铋飑烘矧磲趑弪è衡彗轭礤篌徵瀛箦钿灬礅溽豉疱螬箦翩轶脲黠蜾铋飑箦翩轭礤篌徵瀛箦钿舂ㄣ犰飙骘蝽狒翦ㄣ镱吼狎孱轶豉疱螬┅ê孱洵礤篌徵瀛箦钿灬礅溽豉疱螬箦翩轶脲黠蜾铋飑箦翩轭礤篌徵瀛箦钿铋飑ㄣ犰飙骘蝽狒翦ㄣ镱吼狎孱轶豉疱螬┅ê黠蜾轶灬礅溽豉疱螬ㄤ邈灬蝈ㄩ珙矧豉疱┅痱镧戾è蝈篚祠ㄩㄦ轭洵疳汶徵恒镢镝祜镫躔ㄦ躅汜祆簌礅镬骢钽糸镱ㄩ铘弪⒂偻孪汰滔纤招恒镢镝祜镫躔┅螬┅ㄩ蝈篚祠ㄦ矧磲铋⒓栩彐杰淋沆狍蠼堍簌礅镬堍峻良峋蝈篚祠螬ㄩ礤礅弪悱蝈箦蝣邃黠蜾螵呼弩＇篝蜷铉僵ㄦ矧磲铋⒓箴犷沆狍蠼堍簌礅镬堍峻良箴犷劲螬ㄩ轭礤篌徵瀛箦钿ㄩ轶脲黠蜾ㄦ矧磲铋⒓箴犷沆狍蠼堍脲黠蜾堍峻良箴犷劲螬螬螬┅箦翩轶脲黠蜾铒轶脲黠蜾┅┅┅┅＋铋ㄤ彐疳蜥礤翦铛礅弪螵氨渤吹斗腹＋铋ㄤ彐轭瀛泔祜蜷铉豉疱瑚邂腴⒆邂碎翦矧溟骀吼狎孱轰殒呼蜥铙轸轱铙èê骈戾骝镯è筱犷蝈鲩箝镱箦舡盹溴候弼轶轱乎铘殪筱犷＼┅┅è候弼轶轱瞟è筱犷犷铛礅弪螵箦舡盹溴候弼轶轱瞽铛礅弪┅è候弼轶轱候弼轶轱瞽铛礅弪è筱犷＼铄黛轭濠箦舡盹溴烘轵篝汨狎镱扉铄┅è候弼轶轱瞽铛礅弪è筱犷＼┅箦舡盹溴候弼轶轱瞟┅è轰殒姝徜溴洎è筱犷⒁弼殄麇怡蜗孪馁ㄏ闲印箦舡盹溴猴镳乎铘殪ㄡ漩犷沐暴横漩犷汩铉铋飑┅烘矧磲趑弪èê蝈鲩箝镱灬礅溽豉疱螬ㄤ邈灬蝈ㄩ珙矧豉疱┅ㄦ矧磲铋⒓箴犷沆狍蠼堍篝蜷铉堍峻良箴犷劲螬┅è候弼轶轱瞽铛礅弪灬礅溽豉疱螬ㄤ邈灬蝈ㄩ珙矧豉疱┅ㄦ矧磲铋⒓栩彐杰㈣趑鸷趄徙麇怆轸矧绡汨犷珏箦舣淋沆狍蠼堍溟骀扉铍堍峻邯良峋螬┅è洪钿屮骈戾灬礅溽豉疱螬ㄤ邈灬蝈ㄩ珙矧豉疱┅ㄦ矧磲铋⒓栩彐杰㈣趑鸷趄徙麇怆轸矧绡怛秣箦虔趄躅氙淋沆狍蠼堍溟骀扉铍堍峻邯良峋螬┅è虹轸轭溴骈戾灬礅溽豉疱螬ㄤ邈灬蝈ㄩ珙矧豉疱┅ㄦ矧磲铋⒓栩彐杰㈣趑鸷趄徙麇怆轸矧绡怛秣箦虔趄躅氙淋沆狍蠼堍溟骀扉铍堍峻良峋ㄩ戾铉翳螬博篚怏羼博螬螬┅è猴镳螬灬礅溽豉疱螬ㄤ邈灬蝈ㄩ珙矧豉疱┅ㄦ矧磲铋⒓箴犷沆狍蠼堍簌铘狲弪蝻蜍⒕良箴犷劲螬┅┅