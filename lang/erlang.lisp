;; lang/erlang.lisp

(in-package :colorize)

(defvar *erlang-open-parens* "([{")
(defvar *erlang-close-parens* ")]}")

(defvar *erlang-reserved-words*
  '("after" "andalso" "begin" "catch" "case" "end" "fun" "if" "of" "orelse"
    "receive" "try" "when" "query" "is_atom" "is_binary" "is_constant"
    "is_float" "is_function" "is_integer" "is_list" "is_number" "is_pid"
    "is_port" "is_reference" "is_tuple" "is_record" "abs" "element" "float"
    "hd" "tl" "length" "node" "round" "self" "size" "trunc" "alive" "apply"
    "atom_to_list" "binary_to_list" "binary_to_term" "concat_binary"
    "date" "disconnect_node" "erase" "exit" "float_to_list" "garbage_collect"
    "get" "get_keys" "group_leader" "halt" "integer_to_list" "internal_bif"
    "link" "list_to_atom" "list_to_binary" "list_to_float" "list_to_integer"
    "make_ref" "node_link" "node_unlink" "notalive" "open_port" "pid_to_list"
    "process_flag" "process_info" "processes" "put" "register" "registered"
    "setelement" "spawn" "spawn_link" "split_binary" "statistics"
    "term_to_binary" "time" "throw" "trace" "trunc" "tuple_to_list"
    "unlink" "unregister" "whereis"))

(defparameter *erlang-begin-word* "abcdefghijklmnopqrstuvwxyzABCDEFGHIJKLMNOPQRSTUVWXYZ_0123456789")
(defparameter *erlang-begin-fun* "abcdefghijklmnopqrstuvwxyz")
(defparameter *erlang-begin-var* "ABCDEFGHIJKLMNOPQRSTUVWXYZ_")
(defparameter *erlang-terminators* '(#\space #\return #\tab #\newline #\. #\; #\, #\/ #\- #\* #\+ #\( #\) #\' #\" #\[ #\] #\< #\> #\{ #\}))

(define-coloring-type :erlang "Erlang"
  :default-mode :first-char-on-line
  :transitions
  (((:normal :paren-ish)
    ((scan "%")
     (set-mode :comment
               :until (scan #\newline)))
    ((scan-any *erlang-begin-var*)
     (set-mode :variable
               :until (scan-any *erlang-terminators*)
               :advancing nil))
    ((scan-any *erlang-begin-word*)
     (set-mode :word-ish
               :until (scan-any *erlang-terminators*)
               :advancing nil))
    ((or
      (scan-any *erlang-open-parens*)
      (scan-any *erlang-close-parens*))
     (set-mode :paren-ish
               :until (advance 1)
               :advancing nil))
    ((scan #\")
     (set-mode :string
               :until (scan #\")))
    ((scan #\')
     (set-mode :atom
               :until (scan #\')))
    ((scan #\?)
     (set-mode :macro
               :until (scan-any *erlang-terminators*)))
    ((scan #\$)
     (set-mode :char
               :until (scan-any *erlang-terminators*)))
    ((scan #\newline)
     (set-mode :first-char-on-line)))
   
   ((:function :attribute)
    ((or
      (scan-any *erlang-open-parens*)
      (scan-any *erlang-close-parens*))
     (set-mode :paren-ish
               :until (advance 1)
               :advancing nil))
    ((scan-any *erlang-terminators*)
     (set-mode :normal
               :until (scan #\newline))))
   
   (:first-char-on-line
    ((scan "%")
     (set-mode :comment
               :until (scan #\newline)))
    ((scan-any *erlang-begin-fun*)
     (set-mode :function
               :until (scan #\newline)
               :advancing nil))
    ((scan "-")
     (set-mode :attribute
               :until (scan #\newline)
               :advancing nil))
    ((advance 1)
     (set-mode :normal
               :until (scan #\newline))))
   (:string
    ((scan #\\)
     (set-mode :single-escape
               :until (advance 1)))))
  :formatter-variables
  ((paren-counter 0))
  :formatter-after-hook (lambda nil
                          (format nil "窿祜镳骘骝镯疳蝈瞽泔躅翦滹黝麸泔祆邈⒓箴犷炯箴犷劲┅烘矧磲趑弪èê铒蝽犰烘轵篝汨狎镱扉铄灬礅溽豉疱螬ㄤ邈灬蝈ㄩ珙矧豉疱┅螬ê泔眄孱灬礅溽豉疱螬ㄤ邈灬蝈ㄩ珙矧豉疱┅ㄦ矧磲铋⒓箴犷沆狍蠼堍泔眄孱糗⒕良箴犷劲螬┅ê篝蜷铉灬礅溽豉疱螬ㄤ邈灬蝈ㄩ珙矧豉疱┅ㄦ矧磲铋⒓箴犷沆狍蠼堍篝蜷铉堍峻良箴犷劲螬┅ê鲠蜷徕戾灬礅溽豉疱螬ㄤ邈灬蝈ㄩ珙矧豉疱┅ㄦ矧磲铋⒓箴犷沆狍蠼堍鲠蜷徕戾堍峻良箴犷劲螬┅ê骢钽糸镱灬礅溽豉疱螬ㄤ邈灬蝈ㄩ珙矧豉疱┅ㄦ矧磲铋⒓箴犷沆狍蠼堍骢钽糸镱堍峻良箴犷劲螬┅ê狒趄殁豸灬礅溽豉疱螬ㄤ邈灬蝈ㄩ珙矧豉疱┅ㄦ矧磲铋⒓箴犷沆狍蠼堍狒趄殁豸遘⒕良箴犷劲螬┅ê磲泸灬礅溽豉疱螬ㄤ邈灬蝈ㄩ珙矧豉疱┅ㄦ矧磲铋⒓箴犷沆狍蠼堍磲泸镘⒕良箴犷劲螬┅ê狒镯灬礅溽豉疱螬ㄤ邈灬蝈ㄩ珙矧豉疱┅ㄦ矧磲铋⒓箴犷沆狍蠼堍狒镯堍峻良箴犷劲螬┅ê汨狎灬礅溽豉疱螬ㄤ邈灬蝈ㄩ珙矧豉疱┅ㄦ矧磲铋⒓箴犷沆狍蠼堍汨狎徙翦蜍⒕良箴犷劲螬┅ê箝铉戾弩汜疱灬礅溽豉疱螬ㄣ犰飙骘蝽狒翦ㄣ潋豉疱螬┅ê疳蝈瞽轶灬礅溽豉疱螬ㄤ邈灬蝈ㄩ珙矧豉疱┅戾è镳孱铋飑ㄣ秕铘癌ㄩㄥ耢戾铉翳螬暴痱镧麒孱礤礅弪ㄥ祠癌ㄣ镥蜚弪灬铉镳孱疳蝈铙ъ轶舂箦翩镳孱舂箦翩泔躅盹疳蝈瞽泔躅翦订ㄩ钽疳蝈瞽泔躅翦颟麒孱礤礅弪ㄥ祠癌ㄣ镥蜚弪灬铉沆矬瀛疳蝈铙ъ轶舂箦翩镳孱铋飑ㄤ邈疳蝈瞽泔躅翦颟箦翩泔躅盹疳蝈瞽泔躅翦订┅ㄩ镳孱ㄦ矧磲铋⒓箴犷沆狍蠼堍疳蝈铪淋⒕良箴犷沆狍蠼堍淋⒕ū泔躅舂泱蟓忉汶珧秕钿沆狍螵ㄦ矧磲铋⒓箴犷峻良箴犷劲螬┅螬┅ê黠蜾轶灬礅溽豉疱螬ㄤ邈灬蝈ㄩ珙矧豉疱┅ㄩ礤礅弪弪灬铉蝈箦蝣邃黠蜾螵呼弩＇篝蜷铉僵ㄦ矧磲铋⒓箴犷沆狍蠼堍簌礅镬堍峻良箴犷劲螬螬┅┅